plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}
import java.util.Properties

def admobProperties = new Properties()
def admobPropertiesFile = rootProject.file("admob.properties")
if (admobPropertiesFile.exists()) {
    admobPropertiesFile.withInputStream { stream -> admobProperties.load(stream) }
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file("keystore.properties")
if (keystorePropertiesFile.exists()) {
    keystorePropertiesFile.withInputStream { stream -> keystoreProperties.load(stream) }
}
def hasKeystoreConfig = keystorePropertiesFile.exists()

def getAdUnit = { key, fallback ->
    admobProperties.getProperty(key) ?: fallback
}

def releaseBannerAdUnitId = getAdUnit("BANNER_AD_UNIT_ID", "ca-app-pub-2306031288390302/1773448337")
def debugBannerAdUnitId = getAdUnit("BANNER_AD_UNIT_ID_DEBUG", "ca-app-pub-3940256099942544/6300978111")
def releaseInterstitialAdUnitId = getAdUnit("INTERSTITIAL_AD_UNIT_ID", "ca-app-pub-2306031288390302/5943685681")
def debugInterstitialAdUnitId = getAdUnit("INTERSTITIAL_AD_UNIT_ID_DEBUG", "ca-app-pub-3940256099942544/1033173712")

android {
    signingConfigs {
        release {
            if (hasKeystoreConfig) {
                storeFile = file(keystoreProperties.getProperty("storeFile", "keystore.jks"))
                storePassword = keystoreProperties.getProperty("storePassword")
                keyAlias = keystoreProperties.getProperty("keyAlias")
                keyPassword = keystoreProperties.getProperty("keyPassword")
            }
        }
    }
    namespace = "com.workwavestudio.audiocutter"
    compileSdk = 35

    defaultConfig {
        applicationId = "com.workwavestudio.audiocutter"
        // Min API abbassata per massima compatibilit√† con Compose/ffmpeg-kit
        minSdk = 24
        targetSdk = 35
        versionCode = 5
        versionName = "1.0.5"

        vectorDrawables {
            useSupportLibrary = true
        }
        ndk {
            abiFilters "arm64-v8a", "armeabi-v7a"
        }
    }

    buildTypes {
        release {
            minifyEnabled = true
            shrinkResources = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (hasKeystoreConfig) {
                signingConfig = signingConfigs.release
            }
            buildConfigField "String", "BANNER_AD_UNIT_ID", "\"${releaseBannerAdUnitId}\""
            buildConfigField "String", "INTERSTITIAL_AD_UNIT_ID", "\"${releaseInterstitialAdUnitId}\""
        }
        debug {
            buildConfigField "String", "BANNER_AD_UNIT_ID", "\"${debugBannerAdUnitId}\""
            buildConfigField "String", "INTERSTITIAL_AD_UNIT_ID", "\"${debugInterstitialAdUnitId}\""
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }
    buildFeatures {
        compose = true
        buildConfig = true
    }
    sourceSets {
        main {
            jniLibs.srcDirs += ["src/main/cpp/jniLibs"]
        }
    }
    externalNativeBuild {
        cmake {
            path = file("src/main/cpp/CMakeLists.txt")
            version = "3.22.1"
        }
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.1"
    }
    lint {
        abortOnError = false
        checkReleaseBuilds = false
    }
    // Per ridurre dimensioni: APK per ABI, no universal
    splits {
        abi {
            enable = false
            reset()
            include 'arm64-v8a', 'armeabi-v7a'
            universalApk = false
        }
    }
    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
            pickFirsts += ["**/libc++_shared.so"]
        }
        resources {
            excludes += ["/META-INF/{AL2.0,LGPL2.1}", "META-INF/LICENSE*", "META-INF/NOTICE*"]
        }
    }
}

dependencies {
    def composeBom = platform("androidx.compose:compose-bom:2023.08.00")
    implementation composeBom
    androidTestImplementation composeBom

    implementation "androidx.core:core-ktx:1.12.0"
    implementation "androidx.core:core-splashscreen:1.0.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.6.2"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.6.2"
    implementation "androidx.activity:activity-compose:1.7.2"
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.material3:material3"
    implementation "androidx.compose.material:material-icons-extended"
    implementation "androidx.compose.foundation:foundation"
    implementation "androidx.navigation:navigation-compose:2.5.3"
    implementation "com.google.android.material:material:1.11.0"
    implementation 'com.moizhassan.ffmpeg:ffmpeg-kit-16kb:6.0.0'
    // Usa l'AAR locale di ffmpeg-kit full GPL 6.0.2.LTS in app/libs
    //implementation files("libs/ffmpeg-kit-full-gpl-6.0-2.LTS.aar")
    implementation "com.arthenica:smart-exception-java:0.2.1"
    
    implementation "com.google.android.gms:play-services-ads:22.6.0"
    implementation "com.google.accompanist:accompanist-flowlayout:0.28.0"

    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.1"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4"
    debugImplementation "androidx.compose.ui:ui-tooling"
    debugImplementation "androidx.compose.ui:ui-test-manifest"
}
